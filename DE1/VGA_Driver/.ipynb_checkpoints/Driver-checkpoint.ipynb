{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# VGA Driver in Verilog\n",
    "#### V. Hunter Adams (vha3@cornell.edu)\n",
    "\n",
    "> - [The VGA Standard](#The-VGA-Standard)\n",
    "> - [Verilog VGA Driver](#Verilog-VGA-Driver)\n",
    "> - [Instantiating and using the driver on the DE1-SoC](#Instantiating-and-using-the-driver-on-the-DE1-SoC)\n",
    "> - [Example](#Example)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The VGA Standard\n",
    "\n",
    "The VGA standard is described on a [webpage for a different project](https://vha3.github.io/Pico/VGA/VGA.html#The-VGA-protocol). That content is copied below so that this webpage can stand alone.\n",
    "\n",
    "Driving a VGA screen requires manipulating two digital synchronization pins and three analog color pins (`RED`, `GREEN`, and `BLUE`). One of the synchronization pins, `HSYNC`, tells the screen when to move to a new row of pixels. The other synchronization pin, `VSYNC`, tells the screen when to start a new frame. The protocol is described below, both textually and visually.\n",
    "\n",
    "1. The VGA pixel clock runs at 25.172 MHz (on my VGA screen, I can get away with 25 MHz)\n",
    "2. Both `HSYNC` and `VSYNC` start in active mode (logic level high)\n",
    "3. `HSYNC` remains in active mode for 640 pixel clock cycles (i.e., one row of the VGA display).\n",
    "4. For each of the 640 clock cycles, the voltages on the `RED`, `GREEN`, and `BLUE` lines are varied between 0 and 0.7V, with each voltage representing the intensity of that particular color for a particular pixel.\n",
    "5. After 640 clock cycles, the `RED`, `GREEN`, and `BLUE` lines are set to 0, and the `HSYNC` line remains high thru its *frontporch* (16 pixel clock cycles).\n",
    "6. `HSYNC` is set to logic-level low for 96 pixel clock cycles (this is the horizontal sync pulse)\n",
    "7. `HSYNC` is set to logic-level high thru its *backporch* (48 pixel clock cycles).\n",
    "8. `HSYNC` then returns to the start of active mode (step 2, above), and the process is repeated for the next row of pixels. Each row of pixels is a *line*.\n",
    "9. `VSYNC` remains in active mode (logic level high) for 480 *lines*.\n",
    "10. After 480 lines, the voltages on the `RED`, `GREEN`, and `BLUE` lines are set to 0, and the `VSYNC` line remains high thru its *frontporch* (10 lines).\n",
    "11. `VSYNC` is set to logic-level low for 2 lines (this is the vertical sync pulse).\n",
    "12. `VSYNC` is set to logic-level high thru its *backporch* (33 lines).\n",
    "13. `VSYNC` then returns to the start of active mode (step 2, above), and the process is repeated for the next frame.\n",
    "\n",
    "<figure>\n",
    "    <img align=\"center\" width=\"800\" height=\"500\" src=\"protocol.png\" alt='missing' />\n",
    "    <center><figcaption>VGA standard</figcaption></center>\n",
    "</figure>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Verilog VGA Driver\n",
    "\n",
    "The VGA driver provided below could be written more efficiently. In particular, some of the horizontal and vertical states in the state machine could be consolidated. I left these states separate so that the below Verilog maps as clearly as possible to the timing diagrams described in the previous section.\n",
    "\n",
    "I chose 8-bit (RRRGGGBB) color because of M10K memory limitations on the DE1-SoC.\n",
    "\n",
    "```verilog\n",
    "module vga_driver (\n",
    "\tinput wire clock,     // 25 MHz\n",
    "\tinput wire reset,     // Active high\n",
    "\tinput [7:0] color_in, // Pixel color data (RRRGGGBB)\n",
    "\toutput [9:0] next_x,  // x-coordinate of NEXT pixel that will be drawn\n",
    "\toutput [9:0] next_y,  // y-coordinate of NEXT pixel that will be drawn\n",
    "\toutput wire hsync,    // HSYNC (to VGA connector)\n",
    "\toutput wire vsync,    // VSYNC (to VGA connctor)\n",
    "\toutput [7:0] red,     // RED (to resistor DAC VGA connector)\n",
    "\toutput [7:0] green,   // GREEN (to resistor DAC to VGA connector)\n",
    "\toutput [7:0] blue,    // BLUE (to resistor DAC to VGA connector)\n",
    "\toutput sync,          // SYNC to VGA connector\n",
    "\toutput clk,           // CLK to VGA connector\n",
    "\toutput blank          // BLANK to VGA connector\n",
    ");\n",
    "\t\n",
    "\t// Horizontal parameters (measured in clock cycles)\n",
    "\tparameter [9:0] H_ACTIVE  =  10'd_639 ;\n",
    "    parameter [9:0] H_FRONT   =  10'd_15 ;\n",
    "    parameter [9:0] H_PULSE   =  10'd_95 ;\n",
    "\tparameter [9:0] H_BACK    =  10'd_47 ;\n",
    "\n",
    "\t// Vertical parameters (measured in lines)\n",
    "    parameter [9:0] V_ACTIVE   =  10'd_479 ;\n",
    "\tparameter [9:0] V_FRONT    =  10'd_9 ;\n",
    "\tparameter [9:0] V_PULSE\t=  10'd_1 ;\n",
    "\tparameter [9:0] V_BACK \t=  10'd_32 ;\n",
    "\n",
    "\t// Parameters for readability\n",
    "\tparameter \tLOW \t= 1'b_0 ;\n",
    "\tparameter \tHIGH\t= 1'b_1 ;\n",
    "\n",
    "\t// States (more readable)\n",
    "\tparameter \t[7:0]\t H_ACTIVE_STATE    = 8'd_0 ;\n",
    "\tparameter \t[7:0] \tH_FRONT_STATE     = 8'd_1 ;\n",
    "\tparameter \t[7:0] \tH_PULSE_STATE \t= 8'd_2 ;\n",
    "\tparameter \t[7:0] \tH_BACK_STATE \t = 8'd_3 ;\n",
    "\n",
    "\tparameter \t[7:0]\t V_ACTIVE_STATE    = 8'd_0 ;\n",
    "\tparameter \t[7:0] \tV_FRONT_STATE\t = 8'd_1 ;\n",
    "\tparameter \t[7:0] \tV_PULSE_STATE \t= 8'd_2 ;\n",
    "\tparameter \t[7:0] \tV_BACK_STATE \t = 8'd_3 ;\n",
    "\n",
    "\t// Clocked registers\n",
    "\treg \t\t     hysnc_reg ;\n",
    "\treg \t\t     vsync_reg ;\n",
    "\treg \t[7:0]\tred_reg ;\n",
    "\treg \t[7:0]\tgreen_reg ;\n",
    "\treg \t[7:0]\tblue_reg ;\n",
    "\treg \t\t     line_done ;\n",
    "\n",
    "\t// Control registers\n",
    "\treg \t[9:0] \th_counter ;\n",
    "\treg \t[9:0] \tv_counter ;\n",
    "\n",
    "\treg \t[7:0]\t h_state ;\n",
    "\treg \t[7:0]\t v_state ;\n",
    "\n",
    "\t// State machine\n",
    "\talways@(posedge clock) begin\n",
    "\t\t// At reset . . .\n",
    "  \t\tif (reset) begin\n",
    "\t\t\t// Zero the counters\n",
    "\t\t\th_counter \t<= 10'd_0 ;\n",
    "\t\t\tv_counter \t<= 10'd_0 ;\n",
    "\t\t\t// States to ACTIVE\n",
    "\t\t\th_state \t<= H_ACTIVE_STATE  ;\n",
    "\t\t\tv_state \t<= V_ACTIVE_STATE  ;\n",
    "\t\t\t// Deassert line done\n",
    "\t\t\tline_done \t<= LOW ;\n",
    "  \t\tend\n",
    "  \t\telse begin\n",
    "\t\t\t//////////////////////////////////////////////////////////////////////////\n",
    "\t\t\t///////////////////////// HORIZONTAL /////////////////////////////////////\n",
    "\t\t\t//////////////////////////////////////////////////////////////////////////\n",
    "\t\t\tif (h_state == H_ACTIVE_STATE) begin\n",
    "\t\t\t\t// Iterate horizontal counter, zero at end of ACTIVE mode\n",
    "\t\t\t\th_counter <= (h_counter==H_ACTIVE)?10'd_0:(h_counter + 10'd_1) ;\n",
    "\t\t\t\t// Set hsync\n",
    "\t\t\t\thysnc_reg <= HIGH ;\n",
    "\t\t\t\t// Deassert line done\n",
    "\t\t\t\tline_done <= LOW ;\n",
    "\t\t\t\t// State transition\n",
    "\t\t\t\th_state <= (h_counter == H_ACTIVE)?H_FRONT_STATE:H_ACTIVE_STATE ;\n",
    "\t\t\tend\n",
    "\t\t\tif (h_state == H_FRONT_STATE) begin\n",
    "\t\t\t\t// Iterate horizontal counter, zero at end of H_FRONT mode\n",
    "\t\t\t\th_counter <= (h_counter==H_FRONT)?10'd_0:(h_counter + 10'd_1) ;\n",
    "\t\t\t\t// Set hsync\n",
    "\t\t\t\thysnc_reg <= HIGH ;\n",
    "\t\t\t\t// State transition\n",
    "\t\t\t\th_state <= (h_counter == H_FRONT)?H_PULSE_STATE:H_FRONT_STATE ;\n",
    "\t\t\tend\n",
    "\t\t\tif (h_state == H_PULSE_STATE) begin\n",
    "\t\t\t\t// Iterate horizontal counter, zero at end of H_PULSE mode\n",
    "\t\t\t\th_counter <= (h_counter==H_PULSE)?10'd_0:(h_counter + 10'd_1) ;\n",
    "\t\t\t\t// Clear hsync\n",
    "\t\t\t\thysnc_reg <= LOW ;\n",
    "\t\t\t\t// State transition\n",
    "\t\t\t\th_state <= (h_counter == H_PULSE)?H_BACK_STATE:H_PULSE_STATE ;\n",
    "\t\t\tend\n",
    "\t\t\tif (h_state == H_BACK_STATE) begin\n",
    "\t\t\t\t// Iterate horizontal counter, zero at end of H_BACK mode\n",
    "\t\t\t\th_counter <= (h_counter==H_BACK)?10'd_0:(h_counter + 10'd_1) ;\n",
    "\t\t\t\t// Set hsync\n",
    "\t\t\t\thysnc_reg <= HIGH ;\n",
    "\t\t\t\t// State transition\n",
    "\t\t\t\th_state <= (h_counter == H_BACK)?H_ACTIVE_STATE:H_BACK_STATE ;\n",
    "\t\t\t\t// Signal line complete at state transition (offset by 1 for synchronous state transition)\n",
    "\t\t\t\tline_done <= (h_counter == (H_BACK-1))?HIGH:LOW ;\n",
    "\t\t\tend\n",
    "\t\t\t//////////////////////////////////////////////////////////////////////////\n",
    "\t\t\t///////////////////////// VERTICAL ///////////////////////////////////////\n",
    "\t\t\t//////////////////////////////////////////////////////////////////////////\n",
    "\t\t\tif (v_state == V_ACTIVE_STATE) begin\n",
    "\t\t\t\t// increment vertical counter at end of line, zero on state transition\n",
    "\t\t\t\tv_counter<=(line_done==HIGH)?((v_counter==V_ACTIVE)?10'd_0:(v_counter+10'd_1)):v_counter ;\n",
    "\t\t\t\t// set vsync in active mode\n",
    "\t\t\t\tvsync_reg <= HIGH ;\n",
    "\t\t\t\t// state transition - only on end of lines\n",
    "\t\t\t\tv_state<=(line_done==HIGH)?((v_counter==V_ACTIVE)?V_FRONT_STATE:V_ACTIVE_STATE):V_ACTIVE_STATE ;\n",
    "\t\t\tend\n",
    "\t\t\tif (v_state == V_FRONT_STATE) begin\n",
    "\t\t\t\t// increment vertical counter at end of line, zero on state transition\n",
    "\t\t\t\tv_counter<=(line_done==HIGH)?((v_counter==V_FRONT)?10'd_0:(v_counter + 10'd_1)):v_counter ;\n",
    "\t\t\t\t// set vsync in front porch\n",
    "\t\t\t\tvsync_reg <= HIGH ;\n",
    "\t\t\t\t// state transition\n",
    "\t\t\t\tv_state<=(line_done==HIGH)?((v_counter==V_FRONT)?V_PULSE_STATE:V_FRONT_STATE):V_FRONT_STATE;\n",
    "\t\t\tend\n",
    "\t\t\tif (v_state == V_PULSE_STATE) begin\n",
    "\t\t\t\t// increment vertical counter at end of line, zero on state transition\n",
    "\t\t\t\tv_counter<=(line_done==HIGH)?((v_counter==V_PULSE)?10'd_0:(v_counter + 10'd_1)):v_counter ;\n",
    "\t\t\t\t// clear vsync in pulse\n",
    "\t\t\t\tvsync_reg <= LOW ;\n",
    "\t\t\t\t// state transition\n",
    "\t\t\t\tv_state<=(line_done==HIGH)?((v_counter==V_PULSE)?V_BACK_STATE:V_PULSE_STATE):V_PULSE_STATE;\n",
    "\t\t\tend\n",
    "\t\t\tif (v_state == V_BACK_STATE) begin\n",
    "\t\t\t\t// increment vertical counter at end of line, zero on state transition\n",
    "\t\t\t\tv_counter<=(line_done==HIGH)?((v_counter==V_BACK)?10'd_0:(v_counter + 10'd_1)):v_counter ;\n",
    "\t\t\t\t// set vsync in back porch\n",
    "\t\t\t\tvsync_reg <= HIGH ;\n",
    "\t\t\t\t// state transition\n",
    "\t\t\t\tv_state<=(line_done==HIGH)?((v_counter==V_BACK)?V_ACTIVE_STATE:V_BACK_STATE):V_BACK_STATE ;\n",
    "\t\t\tend\n",
    "\n",
    "\t\t\t//////////////////////////////////////////////////////////////////////////\n",
    "\t\t\t//////////////////////////////// COLOR OUT ///////////////////////////////\n",
    "\t\t\t//////////////////////////////////////////////////////////////////////////\n",
    "            // Assign colors if in active mode\n",
    "\t\t\tred_reg<=(h_state==H_ACTIVE_STATE)?((v_state==V_ACTIVE_STATE)?{color_in[7:5],5'd_0}:8'd_0):8'd_0 ;\n",
    "\t\t\tgreen_reg<=(h_state==H_ACTIVE_STATE)?((v_state==V_ACTIVE_STATE)?{color_in[4:2],5'd_0}:8'd_0):8'd_0 ;\n",
    "\t\t\tblue_reg<=(h_state==H_ACTIVE_STATE)?((v_state==V_ACTIVE_STATE)?{color_in[1:0],6'd_0}:8'd_0):8'd_0 ;\n",
    "\t\t\t\n",
    " \t \tend\n",
    "\tend\n",
    "\t// Assign output values - to VGA connector\n",
    "\tassign hsync = hysnc_reg ;\n",
    "\tassign vsync = vsync_reg ;\n",
    "\tassign red = red_reg ;\n",
    "\tassign green = green_reg ;\n",
    "\tassign blue = blue_reg ;\n",
    "\tassign clk = clock ;\n",
    "\tassign sync = 1'b_0 ;\n",
    "\tassign blank = hysnc_reg & vsync_reg ;\n",
    "\t// The x/y coordinates that should be available on the NEXT cycle\n",
    "\tassign next_x = (h_state==H_ACTIVE_STATE)?h_counter:10'd_0 ;\n",
    "\tassign next_y = (v_state==V_ACTIVE_STATE)?v_counter:10'd_0 ;\n",
    "\n",
    "endmodule\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Instantiating and using the driver on the DE1-SoC\n",
    "\n",
    "On the DE1-SoC, this module can be used in the following way:\n",
    "\n",
    "1. Create a 25 MHz Phase-Locked-Loop, and attach that to the `clock` input of the module.\n",
    "2. Attach the `reset` input to a register in the module which instantiates the vga driver. Pulse this input high to instantiate the control registers in the driver, then keep it low.\n",
    "3. At every rising edge of the 25 MHz clock, the vga driver will indicate the x and y coordinates for the next pixel that it will draw on the `next_x` and `next_y` outputs from the module.\n",
    "4. *By the next rising edge*, the instantiating module must calculate or lookup the color for that pixel (8-bit, RRRGGGBB) and put that color on the `color_in` input to the module.\n",
    "> - **NOTE**: If you are storing pixel color data in M10k memory, remember that there is a 1-cycle delay associated with memory reads. Thus, the clock for the memory must be running at *at least* 75 MHz.\n",
    "5. All of the other module outputs (`VGA_HS`, `VGA_VS`, `VGA_R`, `VGA_G`, `VGA_B`, `VGA_SYNC_N`, `VGA_CLK`, and `VGA_BLANK_N`) are connected directly to the VGA i/o in the Computer_system module."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Example\n",
    "\n",
    "Here is an example of an instantiation:\n",
    "\n",
    "```verilog\n",
    "// Instantiate VGA driver\t\t\t\t\t\n",
    "vga_driver draw   ( .clock(vga_pll),        // 25 MHz PLL\n",
    "                    .reset(vga_reset),      // Active high reset, manipulated by instantiating module\n",
    "                    .color_in(pixel_color), // Pixel color (RRRGGGBB) for pixel being drawn\n",
    "                    .next_x(next_x),\t\t// X-coordinate (range [0, 639]) of next pixel to be drawn\n",
    "                    .next_y(next_y),\t\t// Y-coordinate (range [0, 479]) of next pixel to be drawn\n",
    "                    .hsync(VGA_HS),         // All of the connections to the VGA screen below\n",
    "\t\t\t\t\t.vsync(VGA_VS),\n",
    "\t\t\t\t\t.red(VGA_R),\n",
    "\t\t\t\t\t.green(VGA_G),\n",
    "\t\t\t\t\t.blue(VGA_B),\n",
    "\t\t\t\t\t.sync(VGA_SYNC_N),\n",
    "\t\t\t\t\t.clk(VGA_CLK),\n",
    "\t\t\t\t\t.blank(VGA_BLANK_N)\n",
    ");\n",
    "```\n",
    "\n",
    "[This project](./VGA_to_M10K.zip) demonstrates the driver. The module which instantiates the driver writes a simple checkerboard pattern (pictured below) to M10K memory. The module uses the `next_x` and `next_y` outputs from the vga driver to set the read address for the memory and access pixel color information. Artifacts are from the camera and are not visible on the VGA. <br><br>\n",
    "\n",
    "<figure>\n",
    "    <img align=\"center\" width=\"500\" height=\"500\" src=\"checkerboard.png\" alt='missing' />\n",
    "    <center><figcaption>VGA driver rendering a checkerboard from M10K memory</figcaption></center>\n",
    "</figure>\n",
    "\n",
    "The Mandelbrot renderer demonstrated below also uses this driver: <br><br>\n",
    "\n",
    "<iframe width=\"560\" height=\"315\" src=\"https://www.youtube.com/embed/s-EJJK9lSTM\" title=\"YouTube video player\" frameborder=\"0\" allow=\"accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture\" allowfullscreen></iframe>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "***\n",
    "***\n",
    "***"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
